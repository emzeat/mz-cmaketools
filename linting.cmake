#
# linting.cmake
#
# Copyright (c) 2013 - 2022 Marius Zwicker
# All rights reserved.
#
# SPDX-License-Identifier: Apache-2.0
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#

##################################################
#
# BUILD/LINTING.CMAKE
#
#   Provides an easy mean to lint and format a file using
#   clang-tidy, clang-format and clazy
#
#   Expects the following variables to be populated in order
#   to pick up the paths of the tools:
#     CLAZY, CLANG_FORMAT, CLANG_TIDY
#
#   Alternatively an attempt will be made to find the
#   package clang-tools-extra
#
#
# PROVIDED MACROS
# -----------------------
# mz_auto_format <target> [<file1> <file2>]...
#   Format and lint the sourcefiles whenever the given target is built.
#   When no explicit sourcefiles are given, all sources the target
#   depends on and ending with (cxx|hpp|cpp|c) will be automatically
#   marked for autoformat
#
########################################################################


########################################################################
## no need to change anything beyond here
########################################################################

mz_include_guard(GLOBAL)
find_package(Git)

# try to gather the executables first
if( NOT CLANG_TIDY AND NOT CLANG_FORMAT )
    find_package(clang-tools-extra QUIET)
    if(NOT clang-tools-extra_FOUND )
      mz_warning_message("clang-tools-extra package is unavailable on this platform - linting will be skipped")
    endif()
    if(TARGET clang-tools-extra::clang-tidy)
      get_property(CLANG_TIDY TARGET clang-tools-extra::clang-tidy PROPERTY IMPORTED_LOCATION)
    endif()
    if(TARGET clang-tools-extra::clang-format)
      get_property(CLANG_FORMAT TARGET clang-tools-extra::clang-format PROPERTY IMPORTED_LOCATION)
    endif()
endif()

# optimize release builds to only lint files changed in the last commit
if( MZ_IS_RELEASE AND GIT_FOUND )
    option(MZ_DO_CPPLINT_DIFF "Run linting on files changed in last commit only" ON)
else()
    option(MZ_DO_CPPLINT_DIFF "Run linting on files changed in last commit only" OFF)
endif()

if( CLANG_TIDY )
    option(MZ_DO_CPPLINT "Enable to run clang-tidy on configured targets" ON)
    set(CMAKE_EXPORT_COMPILE_COMMANDS ON CACHE BOOL "Force enabled by lintin.cmake" FORCE)
endif()
if( CLANG_FORMAT OR CLAZY )
    # default to off in release builds so that we do not alter the code anymore
    if( MZ_IS_RELEASE )
        option(MZ_DO_AUTO_FORMAT "Enable to run clang-format on configured targets" OFF)
    else()
        option(MZ_DO_AUTO_FORMAT "Enable to run clang-format on configured targets" ON)
    endif()
endif()

if( CLANG_TIDY )
  if( MZ_DO_CPPLINT )
    mz_message("Linting (C++) is enabled")
  else()
    mz_warning_message("Linting (C++) is disabled, this is not recommended")
  endif()

  find_program(PYTHON3 python3)
  find_program(CCACHE ccache)
  if(PYTHON3 AND CCACHE)
    mz_message("Linting (C++) will be accelerated using ccache")
    set(MZ_CLANG_TIDY
      ${CMAKE_COMMAND} -E env CLANG_TIDY=${CLANG_TIDY} CCACHE=${CCACHE}
      ${PYTHON3} ${CMAKE_SOURCE_DIR}/build/ccache-tidy.py
    )
  else()
    set(MZ_CLANG_TIDY
      ${CLANG_TIDY}
    )
  endif()
endif()

if( Qt5_PREFIX )
  set(QML_LINT ${Qt5_PREFIX}/bin/qmllint)
  if(NOT EXISTS ${QML_LINT})
      set(QML_LINT FALSE)
  endif()
  set(QML_FORMAT ${Qt5_PREFIX}/bin/qmlformat)
  if(NOT EXISTS ${QML_FORMAT})
      set(QML_FORMAT FALSE)
  endif()
endif()

if( QML_LINT )
  if( MZ_DO_CPPLINT )
    mz_message("Linting (QML) is enabled")
  endif()
endif()

if( MZ_DO_CPPLINT_DIFF )
  mz_message("Linting will run on changed files only")
  # collect the name of the files changed in the last commit
  execute_process(
    COMMAND ${GIT_EXECUTABLE} diff HEAD^ --name-only
    WORKING_DIRECTORY ${CMAKE_SOURCE_DIR}
    OUTPUT_VARIABLE lint_changed_files
    OUTPUT_STRIP_TRAILING_WHITESPACE
  )
  # make the output into CMake list
  string(REPLACE "\n" ";" lint_changed_files ${lint_changed_files})
endif()


macro(mz_auto_format _TARGET)
  set(_sources ${ARGN})
  list(LENGTH _sources arg_count)

  if( NOT arg_count GREATER 0 )
    mz_debug_message("Autoformat was no files given, using the target's sources")
    get_target_property(_sources ${_TARGET} SOURCES)
  endif()

  # filter autogenerated files
  foreach(file ${_sources})
    get_filename_component(abs_file ${file} ABSOLUTE)
    string(REPLACE "${CMAKE_SOURCE_DIR}/" "" rel_file "${abs_file}")
    set(lint_file ${CMAKE_BINARY_DIR}/${rel_file})

    # test if file was changed in last commit
    if( MZ_DO_CPPLINT_DIFF )
      cmake_policy(SET CMP0057 NEW)
      if( rel_file IN_LIST lint_changed_files )
        mz_debug_message("  Linting changed file ${rel_file}")
      else()
        continue()
      endif()
    endif()


    if( NOT ${file} MATCHES "(ui_|moc_|qrc_|lemon_).+" AND NOT "${file}" MATCHES "${CMAKE_BINARY_DIR}" )

      if( ${file} MATCHES ".+\\.(cpp|cxx)$" )
        if( CLANG_TIDY AND MZ_DO_CPPLINT )
          set(lint_output ${lint_file}.clang-tidy)
          add_custom_command(OUTPUT ${lint_output}
            COMMAND ${MZ_CLANG_TIDY}
              ${CLANG_TIDY_EXTRA_ARGS}
              -p ${CMAKE_BINARY_DIR}
              --checks=-clang-diagnostic-unused-command-line-argument
              --quiet
              ${abs_file}
            COMMAND ${CMAKE_COMMAND} -E touch ${lint_output}
            DEPENDS ${CMAKE_SOURCE_DIR}/.clang-tidy ${abs_file}
            COMMAND_EXPAND_LISTS
            COMMENT "Linting (C++) ${rel_file}"
            VERBATIM
          )
          target_sources(${_TARGET}
            PRIVATE ${lint_output}
          )
        endif()
      endif()

      if( ${file} MATCHES ".+\\.(qml)$" )
        if( QML_LINT AND MZ_DO_CPPLINT )
          set(lint_output ${lint_file}.qmllint)
          add_custom_command(OUTPUT ${lint_output}
            COMMAND ${QML_LINT}
              ${abs_file}
            COMMAND ${CMAKE_COMMAND} -E touch ${lint_output}
            DEPENDS ${abs_file}
            COMMAND_EXPAND_LISTS
            COMMENT "Linting (QML) ${rel_file}"
            VERBATIM
          )
          target_sources(${_TARGET}
            PRIVATE ${lint_output}
          )
        endif()
      endif()

      if( ${file} MATCHES ".+\\.(cpp|cxx|hpp|h|c)$" )
        set(format_output ${lint_file}.clang-format)
        if( CLANG_FORMAT AND MZ_DO_AUTO_FORMAT )
          add_custom_command(OUTPUT ${format_output}
            COMMAND ${CLANG_FORMAT}
              -i
              ${abs_file}
            COMMAND ${CMAKE_COMMAND} -E touch ${format_output}
            DEPENDS ${CMAKE_SOURCE_DIR}/.clang-format ${abs_file}
            COMMAND_EXPAND_LISTS
            COMMENT "Formatting ${rel_file}"
            VERBATIM
          )
          target_sources(${_TARGET}
            PRIVATE ${format_output}
          )
        endif()
      endif()

      if( ${file} MATCHES ".+\\.(qml)$" )
        set(format_output ${lint_file}.qmlformat)
        if( QML_FORMAT AND MZ_DO_AUTO_FORMAT )
          add_custom_command(OUTPUT ${format_output}
            COMMAND ${QML_FORMAT}
              -n -i
              ${abs_file}
            COMMAND ${CMAKE_COMMAND} -E touch ${format_output}
            DEPENDS ${abs_file}
            COMMAND_EXPAND_LISTS
            COMMENT "Formatting ${rel_file}"
            VERBATIM
          )
          target_sources(${_TARGET}
            PRIVATE ${format_output}
          )
        endif()
      endif()

    endif()
  endforeach()
endmacro()

macro(mz_auto_format_c _TARGET)
   set(__MZ_NO_CPPLINT TRUE)
   mz_auto_format(${_TARGET} ${ARGN})
endmacro()

macro(mz_auto_format_cxx _TARGET)
   set(__MZ_NO_CPPLINT TRUE)
   mz_auto_format(${_TARGET} ${ARGN})
endmacro()
